plugins {
    id 'java'
    id 'application'
    id 'groovy'
}

repositories {
    mavenCentral()
    maven {
        url = 'https://oss.sonatype.org/content/repositories/snapshots'
        mavenContent {
            includeGroup 'io.github.alopukhov.sybok'
            snapshotsOnly()
        }
    }
    mavenLocal()
}

configurations {
    sybok {
        extendsFrom runtimeClasspath
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    implementation platform('org.junit:junit-bom:5.8.2')

    // dependencies required by out tests
    implementation 'com.google.guava:guava:31.1-jre'
    implementation 'org.spockframework:spock-core:2.1-groovy-3.0'
    implementation 'org.junit.jupiter:junit-jupiter'
    implementation 'junit:junit:4.12'
    runtimeOnly 'org.junit.vintage:junit-vintage-engine'

    // Sybok and JUnit console runtime libs
    sybok 'io.github.alopukhov.sybok:sybok-engine:0.3.0'
    sybok 'org.junit.platform:junit-platform-console'
}

test {
    enabled = false
    useJUnitPlatform()
}

application {
    mainClass = 'org.junit.platform.console.ConsoleLauncher'
    applicationDefaultJvmArgs = ['-Xmx256m', '-Xms256m']
    applicationDistribution.with copySpec {
        into ('specs') {
            from project.layout.projectDirectory.dir("src/test/groovy")
        }
        into ('lib') {
            from configurations.sybok
            duplicatesStrategy = 'EXCLUDE'
        }
    }
}

tasks.named('startScripts', CreateStartScripts).configure {
    classpath = configurations.sybok
    applicationName = "run-launcher"
}
plugins {
    id 'distribution'
    id 'groovy'
}

repositories {
    mavenCentral()
    maven {
        url = 'https://oss.sonatype.org/content/repositories/snapshots'
        mavenContent {
            includeGroup 'io.github.alopukhov.sybok'
            snapshotsOnly()
        }
    }
//    mavenLocal()
}

sourceSets {
    sybok { }
}

dependencies {
    sybokImplementation platform('org.junit:junit-bom:5.8.2')

    // dependencies required by out tests
    sybokImplementation('org.spockframework:spock-core:2.1-groovy-3.0') {
        exclude group: 'org.jetbrains', module: 'annotations'
        exclude group: 'org.junit.platform', module: 'junit-platform-testkit'
        // no mocking in tests
        exclude group: 'cglib'
        exclude group: 'org.objenesis'
        exclude group: 'org.ow2.asm'
        exclude group: 'net.bytebuddy'
    }
    sybokImplementation 'org.junit.jupiter:junit-jupiter'
    sybokImplementation 'junit:junit:4.13.2'

    // dependencies to run tests
    sybokRuntimeOnly 'io.github.alopukhov.sybok:sybok-engine:0.3.0'
    sybokRuntimeOnly 'org.junit.vintage:junit-vintage-engine'
    sybokRuntimeOnly 'org.junit.platform:junit-platform-console'

    modules {
        module("org.hamcrest:hamcrest-core") {
            replacedBy("org.hamcrest:hamcrest")
        }
    }
}

tasks.register('startScripts', CreateStartScripts).configure {
    outputDir = file("${project.buildDir}/scripts")
    classpath = files('*')
    // more conservative option is
    // classpath = configurations.sybokRuntimeClasspath
    applicationName = 'run-junit-console'
    optsEnvironmentVar = 'SYBOK_OPTS'
    mainClass = 'org.junit.platform.console.ConsoleLauncher'
    defaultJvmOpts = ['-Xmx256m']
}

distributions {
    main {
        distributionBaseName = 'sybok-example'
        contents {
            into('lib') {
                from configurations.sybokRuntimeClasspath
            }
            into('bin') {
                from project.tasks.named('startScripts')
            }
            into('tests') {
                from sourceSets.sybok.groovy
            }
        }
    }
}